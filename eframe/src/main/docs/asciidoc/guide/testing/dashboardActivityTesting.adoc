
==== Dashboard Activity Testing

Dashboard activities can be complex and require some extensive testing.  In particular,
event handling is important to these activities.  To make this testing easier,
the framework provides a
link:groovydoc/org/simplemes/eframe/dashboard/controller/DashboardTestController.html[DashboardTestController^]
icon:share-square-o[role="link-blue"] that gives you some helpful activities for testing.
These include:

* <<Dashboard Event Display Helper>> - Displays the events triggered by an activity.
* <<Dashboard Event Trigger Helper>> - Allows the test spec to publish any dashboard event.
* DISPLAY_PARAMETERS_ACTIVITY - Displays the parameters passed to the activity.



===== Dashboard Event Display Helper

This helper displays any events triggered by the dashboard activities.

[source,groovy]
.Example - Dashboard Event Display Helper
----
  def "verify that the selection activity sends the ORDER_LSN_CHANGED event"() {
    given: 'a dashboard with the activity'
    buildDashboard(defaults: ['/selection/workCenterSelection',                 // <.>
                              DashboardTestController.DISPLAY_EVENT_ACTIVITY])  // <.>

    when: 'the dashboard is displayed'
    displayDashboard([workCenter: 'WC137'])

    then: 'the order is changed'               // <.>
    textField('order').input.value('ORDER1')
    sendKey(Keys.TAB)
    waitFor {
      $('#events').text().contains('ORDER_LSN_CHANGED')
    }

    and: 'the event is triggered'   // <.>
    def s = $('#events').text()
    def json = new JsonSlurper().parseText(s)
    json.type == 'ORDER_LSN_CHANGED'
    json.source == '/selection/workCenterSelection'
    json.list.size() == 1
    json.list[0].order == 'ORDER1'
  }
----
<.> The activity under test.
<.> Uses the _DISPLAY_EVENT_ACTIVITY_ in the second panel of the dashboard.
<.> Some user action triggers an event.
<.> The event text is retrieved from the page, parsed and validated.



This _DISPLAY_EVENT_ACTIVITY_ will display the dashboard events in a `<div>` in the page
for easy access by the test spec.  The UI will look something like this:

image::guis/dashboardEventHelperActivity.png[title="dashboardEventHelperActivity",align="center", width="800"]

The event is rendered as JSON text that can be accessed by the test for verification.

===== Dashboard Event Trigger Helper

This helper lets your test trigger a dashboard activity.

[source,groovy]
.Example - Dashboard Event Trigger Helper
----
  def "verify that the selection activity handles the WORK_LIST_SELECTED event"() {
    given: 'a dashboard with the activity'
    buildDashboard(defaults: ['/selection/workCenterSelection',                // <.>
                              DashboardTestController.TRIGGER_EVENT_ACTIVITY]) // <.>

    when: 'the dashboard is displayed'
    displayDashboard()

    and: 'the event is triggered'
    def event = [type: 'WORK_LIST_SELECTED', source: 'abc', list: [[order: 'ORDER1']]] // <.>
    textField("eventSource").input.value(JsonOutput.toJson(event))
    clickButton('triggerEvent')  // <.>
    waitFor {
      textField('order').input.value() == 'ORDER1'  // <.>
    }

    then: 'the field is correct'
    textField('order').input.value() == 'ORDER1'
  }

----
<.> The activity under test.
<.> Uses the _TRIGGER_EVENT_ACTIVITY_ in the second panel of the dashboard.
<.> The JSON for the event _'WORK_LIST_SELECTED'_ is placed in the input field.
<.> The event is triggered with the button in the helper activity.
<.> The test waits for the activity being tested to handle the event.


This _TRIGGER_EVENT_ACTIVITY_ will provide an input field to enter the JSON for the event
to be triggered. The UI will look something like this:

image::guis/dashboardEventTriggerHelperActivity.png[title="dashboardEventTriggerHelperActivity",align="center", width="800"]


