
=== Module Additions

ifeval::["{backend}" != "pdf"]

[inline-toc-header]#Overview#
[inline-toc]#<<When to Modularize>>#
[inline-toc]#<<Module Terminology>>#
[inline-toc]#<<Inheritance?>>#

[inline-toc-header]#Details#
[inline-toc]#<<How to Modularize>>#
[inline-toc]#<<Addition>>#
[inline-toc]#<<Example Addition>>#
[inline-toc]#<<Addition Top-Level Options>>#
[inline-toc]#<<Encoded Types Provided By Additions>>#
[inline-toc]#<<Addition Field Extensions>>#
[inline-toc]#<<Initial Data Loaders and Additions>>#
[inline-toc]#<<Addition Errors>>#

endif::[]


NOTE: This modularization is complex.  Only use it when needed see <<When to Modularize>>.

==== Overview


Module composition allows you to modularize your application into one or more modules that
work together. This can help you provide simplified configurations of your application for
specific customer groups. It also gives your customers flexibility in customization and
deployment of your application.

These modules can work with each other and provide new features to your core application.
This provides the ability to add fields to core domain objects, extend the functionality of service
methods, ask questions in core GUIs and to participate in database transactions with core objects.


An example set of modules used to create an application might look like this:




//workaround for https://github.com/asciidoctor/asciidoctor-pdf/issues/271
:imagesdir: {imagesdir-build}

[ditaa,"architectureLayersComp"]
----
+------------------------------+
| Application (Deployable JAR) |
|                         cGRE |
+---------------+--------------+
|  App Module1  |  App Module2 |
|    (Online)   |   (Retail)   |
|          cBLU |         cBLU |
+---------------+--------------+
|   Application Core Module    |
|                         cBLU |
+------------------------------+
| Enterprise Framework Module  |
|                              |
+------------------------------+
|          Micronaut           |
|                              |
+------------------------------+

----

//end workaround for https://github.com/asciidoctor/asciidoctor-pdf/issues/271
:imagesdir: {imagesdir-src}

This application can be deployed with the optional _Online_ module and/or the
optional _Retail_ module.  This allows your customers to streamline the application to suit
their needs.

For example, you could deploy the application without the _Retail_ module for customers
that have only an online business model.


==== When to Modularize

This modularization is complex and increases your testing needs.  You need to decide if this
flexibility is worth the cost. Like all features, you need to decide if the flexibility for
your customers is worth the extra effort. Sometimes, a simple toggle such as `OrderType`
might meet your needs.  Modularization is normally used when the module adds significant
overhead or complexity to your application.

Modularization is also useful when a module's features are needed by a small subset of your
customers.  This allows you to hide this complexity from most of your customers to simplify
your app.

==== Module Terminology

In general, a module adds features using <<Field Extensions>> <<method-extensions>> added to the
core features.

The enterprise framework supports a declarative way to define these extensions using
an Addition feature that is discovered at run-time. The framework uses the term 'addition' to avoid
confusion with field and method extensions.  An addition is basically a way for a module to extend
another module with minimal coding.  The additions are beans that are created by the framework
upon startup and a stored as singletons.  The addition can also specify new data types and choices
for many drop-down lists.


==== Inheritance?

Why not use inheritance to add features to domains, controllers and services?  The main reason
is that you may need multiple optional modules.  An inheritance hierarchy can't be configured
as optional. For this reason, the <<Field Extensions>> and <<Method Extensions>> mechanisms
are used.


==== How to Modularize

So, you have decided to implement a portion of your application in a module.  How do you do this?
The basic steps are:

* <<Design and Name Your Module>>
* Create a Micronaut Module.  See the <<{mes-core-path}/guide.adoc#,SimpleMES>> application for an example.
* Create an <<Addition>>.

===== Design and Name Your Module

The module's name should be short, simple and descriptive.  This is not always easy.
If needed, use a longer name.  In most IDEs, you won't have to type this name too often.

The design of your module should follow normal <<Guidelines>> for domains, controllers, etc.
This means a simple package scheme and class naming approach.

You will also need to decide how you will extend the core (and other) modules with additional
fields and behavior.

==== Addition

When you create a new module for the framework, you will need to create an _Addition_ class
that describes a few key parts of your module.  This allows for faster startup since
the framework won't have to search the class path for all of you modules features.

The Addition describes elements including:

* Where to find your domain classes.
* Field Extensions
* Encoded Types
* Initial Data Loaders

Additions are defined as a class with a simple DLS (Domain Specific Language) to define
these elements.  Not all are required.

===== Example Addition

Below is a simple addition that adds some fields, domains and other features that an addition
can provide.


[source,groovy]
.Addition Example - Simple Addition
----
@Singleton   // <.>
class SimpleAddition extends BaseAddition implements AdditionInterface {
  AdditionConfiguration addition = Addition.configure {
    encodedType OrderStatus
    initialDataLoader InitialDataLoadRoles
    field { domain Order; name 'warehouse'}  // <.>
    field {
      domain Product
      name 'productCode'
      format LongFieldFormat
      valueClass ProductComponent
      fieldOrder { name 'group:components' }
      fieldOrder { name 'components'; after 'group:components' }
      guiHints """label="Legacy Code" """   // <.>
    }
  }
}
----
<.> Exposes this addition as standard bean.
<.> Single-line format for a field definition.
<.> Provides a GUI display hint for the display <<Markers>>.


This example defines the global features such as where to find domains for the
module, any loader additional <<Encoded Types>> and some custom fields added
to the _Order_ domain.

===== Addition Top-Level Options

The Addition supports these top-level options:

[cols="1,4", width=75%]
.Addition Options
|===
|Option | Description

|name | The name of the addition (*Default*: The addition class's simple name).
|field | Defines a single field added to a domain.  See <<Addition Field Extensions>> (*Optional*).
|encodedType | One of the <<Encoded Types Provided By Additions>> (*Optional*).
|initialDataLoader | A class that is the initial data loader.  See <<Initial Data Loaders and Additions>> (*Optional*).
|===


===== Addition Field Extensions

One of more important reasons to use additions is to add custom fields to core
domain classes in other modules.  This definition is used to create normal
<<Field Extensions>> for the defined fields.  This means your module can add fields
to GUIs in core domains and also import/export the values.


[source,groovy]
.Addition Example - Field Addition
----
@Singleton   // <.>
class SimpleAddition extends BaseAddition implements AdditionInterface {
  AdditionConfiguration addition = Addition.configure {
    field {  // <.>
      domain SampleParent
      name 'priority'
      format LongFieldFormat
      valueClass Order
      fieldOrder { name 'priority'; after 'notes' }
      guiHints """label="Order Priority" """
    }
  }
}
----
<.> Exposes this addition as a standard bean.
<.> Defines a single `priority` field added to the _Order_ class.


These field extensions provide a lot of configuration options:

[cols="1,4", width=75%]
.Addition Field Options
|===
|Option | Description

|domain | The domain class (*Required*).
|name | The name of the field to add to the domain (*Required*).
|label | The label for the field (*Default*: `name`).
|format | The domain class (*Default*: String - no limit).
|maxLength | The max length of the value (*Optional*).  Only applies to String fields at this time.
|valueClass | The class for the value (*Optional*).  This is used mainly for DomainReferences,
              Enumeration and EncodedTypes.
|fieldOrder | Defines a <<Field Ordering>> entry for the domain (*Optional*).  See below.
|guiHints | GUI Hints to add to the display of these additions. (*Optional*).  These
            are typically attributes supported but the <<Markers>> such as <<efCreate>>.
|===


The options supported by the `fieldOrder` element above are:

[cols="1,4", width=50%, align="center"]
.fieldOrder - Options
|===
|fieldOrder | Description

|name | The field to add to the field order (*Required*).
|after | The new field will be added after this field in the display order (*Default*: the end).
|===




===== Encoded Types Provided By Additions

<<Encoded Types>> are used to store encoded values in a column in the database.  These
encoded values are short strings that are resolved by the base class.
Your addition may provide more encoded types by specifying the _encodedType_ element:


[source,groovy]
.Addition Example - Encoded Types Provided by an Addition
----
@Singleton
class SimpleAddition extends BaseAddition implements AdditionInterface {
  AdditionConfiguration addition = Addition.configure {
    encodedType OrderStatus  // <.>
     . . .
  }
}
----
<.> Defines a single base class for a new encoded type.


===== Initial Data Loaders and Additions

Sometimes, a module will need to add records to a core module's database using the framework's
<<Initial Data Load>> mechanism.  This is common with user <<Roles>>.  To avoid creating dummy
domain classes, you can specify a list of classes that perform the initial data load like normal
domain classes.

[source,groovy]
.Addition Example - Initial Data Loaders Provided By Additions
----

@Singleton                            // <.>
class SimpleAddition extends BaseAddition implements AdditionInterface {
  AdditionConfiguration addition = Addition.configure {
    initialDataLoader SetupRoles  // <.>
     . . .
  }
}
. . .

class SetupRoles {
  static initialDataLoad() {  // <.>
   . . .
  }
}


----
<.> Registers this addition within the application context for discovery at runtime.
<.> Specifies the class that performs the initial data loading.
<.> Performs the actual initial data loading.

===== Addition Errors

The Addition syntax can be somewhat complex.  Most errors are caught by the framework at
run-time.  This means the first error will usually show up when you deploy your addition with
the application.

You will probably need to monitor the log files for any ERROR level messages.
Most errors will be logged, but the application startup will still be attempted.

To catch these errors earlier, we suggest that you unit test your addition.  For example:

[source,groovy]
.Addition Example Unit Test
----

def "verify that the addition is valid"() {
  expect: 'the validation passes'
  new MyAddition().addition.validate()
}

----

