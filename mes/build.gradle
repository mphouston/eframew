buildscript {
  repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
  }
}

plugins {
  id "io.spring.dependency-management" version "1.0.6.RELEASE"
  id 'maven-publish'
  id "com.jfrog.bintray" version "1.8.4"
  id "com.bertramlabs.asset-pipeline" version "3.0.10"
  id "org.owasp.dependencycheck" version "4.0.0.1"
  id 'org.asciidoctor.jvm.convert' version '3.1.0'
  id "com.github.johnrengelman.shadow" version "6.1.0"
  id "io.micronaut.application" version '1.0.5'
}

repositories {
  mavenLocal()
  maven {
    url  "https://dl.bintray.com/simplemes/simplemes-core"
  }
  mavenCentral()
  jcenter()

}

version "0.5"
group "org.simplemes"

apply plugin: "application"
apply plugin: "java"
apply plugin: "groovy"

// Common tasks from the overall common.gradle file provides: version, bintrayUpload, maven artifact creation.
apply from: "../gradle/common.gradle"


dependencies {
  implementation ("org.simplemes:webix:$webixVersion")
  implementation ("org.simplemes:eframe:$simplemesVersion")
  implementation ("org.simplemes:mes-core:$simplemesVersion")
  implementation ("org.simplemes:mes-assy:$simplemesVersion")
  // Most dependencies come from the ../gradle/common.gradle file.

}

mainClassName = "org.simplemes.mes.app.Application"

// Local Tasks
// Remove some sample/test files from the published jar.
jar {
  exclude "logback.xml"
  exclude "application-test.yml"
  exclude "/sample/**"
  exclude "/reports/sample/**"
}

// Move the SimpleMES/framework files to a separate layer for shorter docker image updates.
task moveJarLayers(type: Copy) {
  from "$buildDir/layers/libs"
  include "webix-*.jar","eframe-*.jar","mes-*.jar"
  into "$buildDir/layers/internal"
}
task removeInternalJars(type: Delete) {
  delete fileTree("$buildDir/layers/libs").matching {
    include "webix-*.jar","eframe-*.jar","mes-*.jar"
  }
}

// Need build the application.jar ourselves since there appears to be no way to split the libs structure into two
// folders: libs and internal (simplemes modules).  So we just overwrite the application.jar file with the
// split libs.
task rebuildApplicationJar(type: Jar) {
  archiveFileName= "$buildDir/layers/application.jar"
  def cp = fileTree("$buildDir/layers/libs").collect { "libs/"+it.getName() }.join(' ')
  cp += " "+fileTree("$buildDir/layers/internal").collect { "internal/"+it.getName() }.join(' ')
  cp += " resources/"
  manifest {
    attributes(
      "Class-Path": cp,
      "Main-Class": mainClassName)
  }
  from ("$buildDir/classes/groovy/main")
  //sources = fileTree("$buildDir/classes/groovy/main")
}
dockerBuild.dependsOn  moveJarLayers,removeInternalJars,rebuildApplicationJar
removeInternalJars.mustRunAfter moveJarLayers
rebuildApplicationJar.mustRunAfter removeInternalJars

//buildLayers.finalizedBy moveJarLayers,removeInternalJars,printLibs1
//removeInternalJars.finalizedBy printLibs2

// Force the META-INF/services from each micronaut module to be used in the Fat Jar and combine common .properties
// from all modules.
shadowJar {
  append 'assets/manifest.properties'
  append 'i18n/messages.properties'
  mergeServiceFiles()
}


